plugins {
  id "io.spring.dependency-management" version "1.0.4.RELEASE"
}

project.ext {
	mavenLocalRepo = repositories.mavenLocal().url
}
allprojects  {
	apply plugin: 'maven'
	group = 'com.apgsga.patchframework'
	version  = '0.8.5'
}

subprojects {
	apply plugin: 'java'
	apply plugin: "io.spring.dependency-management"
	apply plugin: 'maven-publish'
	apply plugin: 'eclipse'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
	
	eclipse {
    	// TODO
	}
	repositories {
		mavenLocal()
		maven { url "${mavenRepoBaseUrl}/groups/public/" }
		maven { url "${mavenRepoBaseUrl}/repositories/releases/" }
		maven { url "${mavenRepoBaseUrl}/repositories/snapshots/" }
		maven { url "${mavenRepoBaseUrl}/groups/public-snapshots/" }
	}

	dependencyManagement { imports {  mavenBom 'com.affichage.common.maven:dm-bom:9.0.6.ADMIN-UIMIG-SNAPSHOT' } }

	task sourcesJar ( type: Jar) {
		classifier 'sources'
		from sourceSets.main.allJava
	}
	
	publishing {
		publications {
			mavenJava(MavenPublication) { 
				from components.java 
				artifact sourcesJar
			}
		}
		def repoTarget = version.endsWith("SNAPSHOT") ? "snapshots" : "releases"
		repositories {
			maven {
				name ='deployRepo'
				url  "${mavenRepoBaseUrl}/repositories/${repoTarget}/"				
				credentials {
					username = repoUser
					password = repoPassword
				}
			}
		}
	}
	

}
