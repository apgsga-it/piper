import org.apache.tools.ant.filters.*;
plugins {
  	id 'groovy'
    id 'application'
	id 'com.github.johnrengelman.shadow' version '2.0.2'
  	id 'nebula.ospackage' version '4.6.0'
  	id 'ivy-publish' 
}
apply plugin: 'groovy'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'ivy-publish'

description = 'Cli Programm, which provides  Patch Service API through Command Line Interface  '

sourceCompatibility = 1.8
targetCompatibility = 1.8

def finalName = 'apg-patch-cli'

mainClassName = 'pli'
def repoTarget= version.endsWith("SNAPSHOT") ? "snapshots" : "releases"
jar {
	baseName = "${finalName}"
} 

def savedVersion = version

shadowJar {
	baseName = "${finalName}"
	classifier = null
   	version = null
	exclude 'cmd.sh'
	exclude '**/downloads/*'
	exclude '**/uploads/*'
}


dependencies {
	// Application dependencies
	compile project(':apg-patch-service-common')
	compile group: 'org.codehaus.groovy' , name: 'groovy-all', version: '2.4.1'
	compile 'org.codehaus.groovy:groovy-backports-compat23:2.4.5'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.4'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.4'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.4'
	compile("org.jfrog.artifactory.client:artifactory-java-client-services:2.6.0")
	runtime group: 'commons-cli' , name: 'commons-cli', version: '1.4'
	runtime group: 'oracle', name: 'ojdbc'
	testCompile project(':apg-patch-service-server')
	testCompile group: 'org.springframework', name: 'spring-test'
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'	
	testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
	testCompile 'cglib:cglib-nodep:3.2.2' // for stubbing of classes

}

sourceSets {
  test {
    resources {
      srcDir 'src/test/groovy'
      include '**/*.properties'
    }
  }
}

task pkgAsGTar(type: Tar) {
	from "$project.buildDir/${finalName}"
	archiveName = "${finalName}-${version}.tar.gz"
	compression = Compression.GZIP
}

task copyLib( type: Copy ) {
	into "$project.buildDir/${finalName}"
	from shadowJar
	fileMode 0755
	// In lib we only want jars files to go in lib folder (no dll, exe, bat, etc...)
	include "*.jar"
}



def homeDir = "/opt/${finalName}"


// Netflix Nebula Os Package Configuration, see : https://github.com/nebula-plugins/gradle-ospackage-plugin/wiki
ospackage {
	packageName = "${finalName}"
	version = "${savedVersion}"
	release = 2
	os = LINUX
	type = BINARY
	arch = NOARCH
	requires('aps' , "${savedVersion}",  EQUAL)
	
	// Since Package while be used from Jenkins pipeline for writeing and reading revision file
	postInstall 'chown -R jenkins:jenkins /var/opt/apg-patch-cli'

	into "${homeDir}"
	
	from(shadowJar.outputs.files) {
		fileMode 0644
		into 'bin'
	}

	from('packaging/bin') {
		include 'apscli.sh'
		fileMode 0755
		into 'bin'
		//expand project.properties
	}
	
	from('packaging/profile') {
		include 'apscli.sh'
		fileMode 0644
		user 'root'
		permissionGroup 'root'
		into '/etc/profile.d'
		//expand project.properties
	}
	
	from("packaging/conf") {
		into "/etc/opt/${finalName}"
		fileType CONFIG | NOREPLACE
		include 'ops.groovy'
		user 'root'
		permissionGroup 'jenkins'
		addParentDirs false
		fileMode 0740
	}
	
}
 buildRpm {
 	// Parameter are as follow: directory(String path, int permissions, String user, String permissionGroup, boolean addParents)
 	directory ("/var/opt/${finalName}", 0754, "root", "jenkins", false)
}



publishing {
	publications {
	    targz(MavenPublication) {
			artifact source: pkgAsGTar, extension: 'tar.gz'
			artifactId = "${finalName}"
		}
		yumRpm(IvyPublication) {
			artifact buildRpm.outputs.getFiles().getSingleFile()
		}
	}
	repositories {
		maven {
			name ='deployRepo'
			url  "${mavenRepoBaseUrl}/${repoTarget}/"
			credentials {
				username = repoUser
				password = repoPassword
			}
		}
	}
}

artifactoryPublish {}.dependsOn(buildRpm)
artifactory {
    contextUrl = "${mavenRepoBaseUrl}"  
    publish {

        repository {
            repoKey = 'yumrepo'   
            username ="${repoUser}"
            password = "${repoPassword}"
            ivy {
                 artifactLayout = "${buildRpm.outputs.getFiles().getSingleFile().getName()}"
            }
        }
        defaults {
            publications ('yumRpm')
            publishBuildInfo = false  
            publishArtifacts = true   
            publishPom = false   
            publishIvy = false  
        }
    }
}

assemble { dependsOn copyLib,pkgAsGTar}
buildRpm {dependsOn processResources, assemble}


